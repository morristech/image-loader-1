buildscript {
    ext.kotlin_version = '1.2.31'
    ext.support_version = '27.1.1'

    repositories {
        google()
        jcenter()
    }

    dependencies {
        classpath 'com.android.tools.build:gradle:3.1.1'
        classpath 'com.github.dcendents:android-maven-gradle-plugin:2.0'
        classpath 'com.jfrog.bintray.gradle:gradle-bintray-plugin:1.8.0'
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
    }
}

allprojects {
    repositories {
        google()
        jcenter()
    }
}

apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'
apply plugin: 'com.github.dcendents.android-maven'
apply plugin: 'com.jfrog.bintray'

ext {
    libraryName = 'ImageLoader'
    libraryDescription = 'Image loader tool for Android'
    libraryVersion = '2.5.5'
    artifact = 'image-loader'
    developerId = 'yuriy-budiyev'
    developerName = 'Yuriy Budiyev'
    developerEmail = 'yuriy.budiyev@yandex.ru'
    publishedGroupId = 'com.budiyev.android'
    siteUrl = 'https://github.com/yuriy-budiyev/image-loader'
    gitUrl = 'https://github.com/yuriy-budiyev/image-loader.git'
    bintrayRepo = 'maven'
    bintrayName = 'image-loader'
    licenseName = 'MIT License'
    allLicenses = ['MIT']
}

android {
    compileSdkVersion 27

    defaultConfig {
        minSdkVersion 19
        targetSdkVersion 27
        versionCode 87
        versionName libraryVersion
    }

    dexOptions {
        javaMaxHeapSize "2048m"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
}

dependencies {
    api "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
    api "com.android.support:support-annotations:$support_version"
    api "com.android.support:exifinterface:$support_version"
}

group = publishedGroupId

install {
    repositories.mavenInstaller {
        pom {
            project {
                packaging 'aar'
                groupId publishedGroupId
                artifactId artifact
                name libraryName
                description libraryDescription
                url siteUrl

                licenses {
                    license {
                        name licenseName
                    }
                }

                developers {
                    developer {
                        id developerId
                        name developerName
                        email developerEmail
                    }
                }

                scm {
                    connection gitUrl
                    developerConnection gitUrl
                    url siteUrl
                }
            }
        }
    }
}

version = libraryVersion

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from android.sourceSets.main.java.srcDirs
}

task javadoc(type: Javadoc) {
    source = android.sourceSets.main.java.srcDirs
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
    failOnError = false
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.getDestinationDir()
}

artifacts {
    archives javadocJar
    archives sourcesJar
}

Properties properties = new Properties()
properties.load(project.rootProject.file('local.properties').newDataInputStream())

bintray {
    user = properties.getProperty('bintray.user')
    key = properties.getProperty('bintray.apikey')
    configurations = ['archives']

    pkg {
        repo = bintrayRepo
        name = bintrayName
        desc = libraryDescription
        websiteUrl = siteUrl
        vcsUrl = gitUrl
        licenses = allLicenses
        publish = true

        version {
            desc = libraryDescription
            vcsTag = 'v' + libraryVersion
        }
    }
}
